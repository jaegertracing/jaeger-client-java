plugins {
    id "org.jruyi.thrift" version "0.3.1"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.5.0"
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}


ext.opentracingVersion = '0.2.0'
ext.guavaVersion = '18.0'
ext.apacheThriftVersion = '0.9.3'
ext.jerseyVersion = '2.22.2'
ext.slf4jVersion = '1.7.16'
ext.jacksonVersion = '2.7.4'

ext.junitVersion = '4.12'
ext.mockitoVersion = '2.0.2-beta'
ext.powermockVersion = '1.6.4'

allprojects {
    apply plugin: 'idea' // intellij support

    group = 'com.uber.jaeger'
    version = '0.5.1-SNAPSHOT'

    task wrapper(type: Wrapper) {
        gradleVersion = '2.12' //version required
    }
}

subprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    // Set up checkstyle
    apply plugin: 'checkstyle'
    def checkstyleConfigDir = new File(rootDir, "config/checkstyle")
    checkstyle {
        configFile = new File(checkstyleConfigDir, "checkstyle.xml")
        configProperties.checkstyleConfigDir = checkstyleConfigDir
    }

    repositories {
        mavenCentral()
    }

    task listJars(description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }

    license {
        header rootProject.file('license-template')
        strictCheck true
        include '**/*.java'
        exclude '**/gen-java/**'
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Uber Technologies, Inc'
        ignoreFailures = false
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    classes.dependsOn tasks.licenseFormat

    apply from: '../gradle/publish.gradle'
}
