import java.util.regex.Pattern

plugins {
    id "org.jruyi.thrift" version "0.4.0"
    id "jacoco"
    id "com.github.hierynomus.license" version "0.14.0"
    id 'com.github.sherter.google-java-format' version '0.6'
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id "net.ltgt.errorprone" version "0.0.12"
    id 'ru.vyarus.animalsniffer' version '1.4.2'
    id 'io.codearte.nexus-staging' version '0.11.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

ext.developmentVersion = '0.22.0-SNAPSHOT'

ext.opentracingVersion = '0.30.0'
ext.guavaVersion = '18.0'
ext.apacheThriftVersion = '0.9.2'
ext.jerseyVersion = '2.22.2'
ext.slf4jVersion = '1.7.25'
ext.gsonVersion = '2.8.2'
ext.tracerResolverVersion = '0.1.3'

ext.junitVersion = '4.12'
ext.mockitoVersion = '2.12.0'
ext.junitDataProviderVersion = '1.13.1'
ext.awaitilityVersion = '3.0.0'
ext.logbackVersion = '1.2.3'
ext.braveHttpVersion = '4.9.1'

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

allprojects {
    apply plugin: 'idea' // intellij support
    apply plugin: 'jacoco'

    group = 'com.uber.jaeger'
    version = getVersionForBuild()

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'ru.vyarus.animalsniffer'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'


    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    jar {
        from sourceSets.main.output
        manifest {
            attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
        }
    }

    if (!version.endsWith("SNAPSHOT")) {
        nexusStaging {
            packageGroup = "com.uber"
            username = ossrhUsername
            password = ossrhPassword
            // Dealing with error "Wrong number of received repositories in state 'open'" (http://bit.ly/2ybracm)
            numberOfRetries = 50
            delayBetweenRetriesInMillis = 3000
        }

        uploadArchives.finalizedBy closeAndReleaseRepository
    }

    animalsniffer {
        sourceSets = [sourceSets.main]
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    task listJars {
        doLast {
            configurations.compile.each { File file -> println file.name }
        }
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = "7.6.1"
        configProperties.checkstyleSuppressions = "${rootProject.rootDir}/config/checkstyle/suppressions.xml"
    }

    license {
        header rootProject.file('license-template')
        skipExistingHeaders true
        strictCheck true
        include "**/*.java"
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'The Jaeger Authors'
        ignoreFailures = false
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    check.dependsOn tasks.license

    apply from: '../gradle/publish.gradle'
    test {
    testLogging {
        events "failed"
        exceptionFormat "full"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
       }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.18'
        compileOnly 'org.codehaus.mojo:animal-sniffer-annotations:1.16'
    }

}

task codeCoverageReport(type: JacocoReport, group: 'Coverage reports') {
    dependsOn(subprojects*.test)

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/report.xml")
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/crossdock/**',
                              '**/thrift/**',
                              '**/thriftjava/**',
                    ])
        })
    }
}

task printVersion {
    println getVersionForBuild()
}

configure(subprojects.findAll {it.name != 'jaeger-thrift'}) {
   apply plugin: 'net.ltgt.errorprone'
   dependencies {
     errorprone 'com.google.errorprone:error_prone_core:2.1.1'
   }
    tasks.withType(JavaCompile) {
        // These are disabled because of a bug with errorprone. See https://github.com/google/error-prone/issues/750
        options.compilerArgs += [ '-Xep:NestedInstanceOfConditions:OFF',  '-Xep:InstanceOfAndCastMatchWrongType:OFF' ]
    }
}

def getVersionForBuild() {
    String candidateVersion = System.env.TRAVIS_TAG

    if (null == candidateVersion || candidateVersion.isEmpty()) {
        return ext.developmentVersion
    }

    if (Pattern.compile("[v]?((\\d+)\\.(\\d+)\\.(\\d+))").matcher(candidateVersion).matches()) {
        return candidateVersion.drop(1)
    }

    return ext.developmentVersion
}
