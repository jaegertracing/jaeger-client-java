plugins {
    id "org.jruyi.thrift" version "0.3.1"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.7.1"
    id "com.github.hierynomus.license" version "0.12.1"
    id 'com.github.sherter.google-java-format' version '0.3.2'
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id "net.ltgt.errorprone" version "0.0.8"
}


ext.opentracingVersion = '0.15.0'
ext.guavaVersion = '18.0'
ext.apacheThriftVersion = '0.9.2'
ext.jerseyVersion = '2.22.2'
ext.slf4jVersion = '1.7.16'
ext.jacksonVersion = '2.7.4'
ext.apacheHttpComponentsVersion = '4.1.2'

ext.junitVersion = '4.12'
ext.mockitoVersion = '2.2.28'

allprojects {
    apply plugin: 'idea' // intellij support

    group = 'com.uber.jaeger'
    version = '0.18.0-SNAPSHOT'

    task wrapper(type: Wrapper) {
        gradleVersion = '2.12' //version required
    }
}


subprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    task listJars(description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }
    
    task verifyFormatting(type: com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat) {
        exclude '**/gen-java/**'
    }

    license {
        header rootProject.file('license-template')
        strictCheck true
        include "**/*.java"
        excludes(["**/gen-java/**"])
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Uber Technologies, Inc'
        ignoreFailures = false
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    classes.dependsOn tasks.licenseFormat
    classes.dependsOn tasks.verifyFormatting

    apply from: '../gradle/publish.gradle'
    test {
    testLogging {
        events "failed"
        exceptionFormat "full"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
       }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.10'
    }
}

configure(subprojects.findAll {it.name != 'jaeger-thrift'}) {
   apply plugin: 'net.ltgt.errorprone'
}
