apply plugin: 'org.jruyi.thrift'
apply plugin: 'com.github.johnrengelman.shadow'

//This is in a separate subproject so that we can use 
//standard javac instead of the error prone compiler
description = 'Generated thrift code'

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: apacheThriftVersion
}

compileThrift {
    sourceDir "${projectDir}/../idl/thrift"
    outputDir 'src/main/gen-java'
    generator 'java', 'private-members'
    createGenFolder false
}

javadoc {
    exclude '**/gen-java/**'
    exclude '**/com/twitter/zipkin/thriftjava/**'
    exclude '**/com/uber/jaeger/agent/thrift/**'
    exclude '**/com/uber/jaeger/thrift/sampling_manager/**'
    exclude '**/com/uber/jaeger/crossdock/thrift/**'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen-java'
        }
        resources {
            srcDirs = ['src/main/gen-java']
        }
    }

}

jar {
    from sourceSets.main.output
    manifest {
        attributes('Implementation-Title': 'jaeger-thrift', 'Implementation-Version': project.version)
    }
}

shadowJar {
    baseName = 'jaeger-thrift'
    relocate 'org.apache.thrift', 'org.shadow.apache.thrift92'
    classifier 'thrift92'
}

artifacts {
    archives(shadowJar.archivePath) {
        builtBy shadowJar
    }
}
