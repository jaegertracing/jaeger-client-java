apply plugin: 'org.jruyi.thrift'
apply plugin: 'com.github.johnrengelman.shadow'

//This is in a separate subproject so that we can use 
//standard javac instead of the error prone compiler
description = 'Generated thrift code'

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: apacheThriftVersion

    signature 'org.codehaus.mojo.signature:java16:1.1@signature'
}

/**
 * Note that you need to install thrift 0.9.2 on your system for this task to work.
 *
 * If you are using macOS, you can install this specific version via homebrew like so:
 * `brew install https://raw.githubusercontent.com/Homebrew/homebrew-core/56d8c1eba1e5ac30290dd0c486f4bba37f821e42/Formula/thrift.rb`
 */
compileThrift {
    sourceDir "${projectDir}/../idl/thrift"
    outputDir 'src/main/gen-java'
    generator 'java', 'private-members'
    createGenFolder false
}

tasks.withType(Javadoc) {
   enabled = false
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen-java'
        }
    }
}

licenseMain.enabled = false
checkstyleMain.enabled = false
checkstyleTest.enabled = false

shadowJar {
    baseName = 'jaeger-thrift'
    relocate 'org.apache.thrift', 'org.shadow.apache.thrift92'
    classifier 'thrift92'
}

artifacts {
    archives(shadowJar.archivePath) {
        builtBy shadowJar
    }
}
