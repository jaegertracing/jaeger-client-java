apply plugin: 'org.jruyi.thrift'
apply plugin: 'com.github.johnrengelman.shadow'

description = 'Library to send data to Jaeger backend components via Thrift'

dependencies {
    compile project(':jaeger-core')

    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile(group: 'org.apache.thrift', name: 'libthrift', version: apacheThriftVersion) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: junitDataProviderVersion
    testCompile group: 'org.awaitility', name: 'awaitility', version: awaitilityVersion
    testCompile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-grizzly2', version: jerseyVersion

    signature 'org.codehaus.mojo.signature:java16:1.1@signature'
}

compileThrift {
    thriftExecutable "${projectDir}/../scripts/docker-thrift/thrift"
    sourceDir "${projectDir}/../idl/thrift"
    outputDir 'src/main/gen-java'
    generator 'java', 'private-members'
    createGenFolder false
}

tasks.withType(Javadoc) {
    enabled = false
}

tasks.withType(Jar) {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version, 'Specification-Version': project.version)
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen-java'
        }
    }
}

licenseMain.enabled = false
checkstyleMain.enabled = false
checkstyleTest.enabled = false

shadowJar {
    relocate 'com.google.gson'  , 'io.jaegertracing.vendor.com.google.gson'
    relocate 'com.twitter'      , 'io.jaegertracing.vendor.com.twitter'
    relocate 'okhttp'           , 'io.jaegertracing.vendor.okhttp'
    relocate 'okio'             , 'io.jaegertracing.vendor.okio'
    relocate 'org.apache'       , 'io.jaegertracing.vendor.org.apache'
    relocate 'kotlin'           , 'io.jaegertracing.vendor.kotlin'
    relocate 'org.intellij'     , 'io.jaegertracing.vendor.org.intellij'
    relocate 'org.jetbrains'    , 'io.jaegertracing.vendor.org.jetbrains'
    relocate 'javax'            , 'io.jaegertracing.vendor.javax'
    classifier 'shadow'
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api'))
        exclude(dependency('org.projectlombok:lombok'))
        exclude(dependency('org.codehaus.mojo:animal-sniffer-annotations'))
    }
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    archives(shadowJar.archivePath) {
        builtBy shadowJar
    }
    tests testJar
}
