apply plugin: 'org.jruyi.thrift'
apply plugin: 'com.github.johnrengelman.shadow'

//This is in a separate subproject so that we can use 
//standard javac instead of the error prone compiler
description = 'Generated thrift code'

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: apacheThriftVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion

    signature 'org.codehaus.mojo.signature:java16:1.1@signature'
}

compileThrift {
    thriftExecutable "${projectDir}/../travis/docker-thrift/thrift"
    sourceDir "${projectDir}/../idl/thrift"
    outputDir 'src/main/gen-java'
    generator 'java', 'private-members'
    createGenFolder false
}

tasks.withType(Javadoc) {
   enabled = false
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen-java'
        }
    }
}

licenseMain.enabled = false
checkstyleMain.enabled = false
checkstyleTest.enabled = false

shadowJar {
    baseName = 'jaeger-thrift'
    relocate 'org.apache.thrift', 'org.shadow.apache.thrift92'
    classifier 'thrift92'
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    archives(shadowJar.archivePath) {
        builtBy shadowJar
    }
    tests testJar
}
