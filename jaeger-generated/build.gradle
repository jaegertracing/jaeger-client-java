apply plugin: 'org.jruyi.thrift'

description = 'Core library for jaeger-client'

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: apacheThriftVersion
}

compileThrift {
    sourceDir "${projectDir}/../idl/thrift"
    outputDir 'src/main/gen-java'
    generator 'java', 'private-members'
    createGenFolder false
}

javadoc {
    exclude '**/gen-java/**'
    exclude '**/com/twitter/zipkin/thriftjava/**'
    exclude '**/com/uber/jaeger/agent/thrift/**'
    exclude '**/com/uber/jaeger/thrift/sampling_manager/**'
    exclude '**/com/uber/jaeger/crossdock/thrift/**'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/gen-java'
        }
        resources {
            srcDirs = ['src/main/gen-java']
        }
    }

}

jar {
    from sourceSets.main.output
    manifest {
        attributes('Implementation-Title': 'jaeger-generated', 'Implementation-Version': project.version)
    }
}

task jaegerVersion << {
    def propFile = new File(projectDir,
            'src/main/gen-java/com/uber/jaeger/jaeger.properties'
    ).getAbsolutePath()
    new File(propFile).write("jaeger.version=${project.version}")
}

compileJava.dependsOn jaegerVersion
